source ~/dotfiles/env

export GOPATH=$HOME/go
export GOPRIVATE="github.com/hllewelyn/*"

if type nvim &> /dev/null; then
  alias v="vim"
  alias vim="nvim"
  export EDITOR="nvim"
  export PSQL_EDITOR="nvim -c"set filetype=sql""
  export GIT_EDITOR="nvim"
else
  export EDITOR="vim"
  export PSQL_EDITOR="vim -c"set filetype=sql""
  export GIT_EDITOR="vim"
fi

export RUBY_YJIT_ENABLE=1
export DISABLE_SPRING=true

# Silence direnv
export DIRENV_LOG_FORMAT=

eval "$(direnv hook zsh)"

if type brew &>/dev/null; then
  FPATH=$(brew --prefix)/share/zsh/site-functions:$FPATH

  autoload -Uz compinit
  compinit
fi

# aliases
source $HOME/.zsh/aliases

bindkey -v # set to vi bindings
#
# CTRL-R to search through history
bindkey '^R' history-incremental-search-backward
# CTRL-S to search forward in history
bindkey '^S' history-incremental-search-forward
# Accept the presented search result
bindkey '^Y' accept-search

# Use the arrow keys to search forward/backward through the history,
# using the first word of what's typed in as search word
bindkey '^[[A' history-search-backward
bindkey '^[[B' history-search-forward

# Use the same keys as bash for history forward/backward: Ctrl+N/Ctrl+P
bindkey '^P' history-search-backward
bindkey '^N' history-search-forward

# Backspace working the way it should
bindkey '^?' backward-delete-char
bindkey '^[[3~' delete-char

# Autoload zsh add-zsh-hook and vcs_info functions (-U autoload w/o substition, -z use zsh style)
autoload -Uz add-zsh-hook vcs_info
# Enable substitution in the prompt.
setopt prompt_subst
# Run vcs_info just before a prompt is displayed (precmd)
add-zsh-hook precmd vcs_info
# add ${vcs_info_msg_0} to the prompt
# e.g. here we add the Git information in green
PROMPT='%1~ %F{green}${vcs_info_msg_0_}%f %# '

setopt autocd # Make CD unnecessary

# Enable checking for (un)staged changes, enabling use of %u and %c
zstyle ':vcs_info:*' check-for-changes true
# Set custom strings for an unstaged vcs repo changes (*) and staged changes (+)
zstyle ':vcs_info:*' unstagedstr ' *'
zstyle ':vcs_info:*' stagedstr ' +'
# Set the format of the Git information for vcs_info
zstyle ':vcs_info:git:*' formats       '(%b%u%c)'
zstyle ':vcs_info:git:*' actionformats '(%b|%a%u%c)'

###############
## Functions
###############

function wip() {
  git commit -m "WIP: $1"
}

start_postgres() {
  if pg_ctl status >/dev/null 2>&1; then
    echo "PostgreSQL is already running"
  else
    pg_ctl start
  fi
}

stop_postgres() {
  if pg_ctl status >/dev/null 2>&1; then
    pg_ctl stop
  else
    echo "PostgreSQL is not running"
  fi
}

start_redis() {
  if pgrep redis-server >/dev/null; then
    echo "Redis is already running"
  else
    redis-server &
  fi
}

# [f]uzzy check[o]ut
fo() {
  git branch --sort=-committerdate --format='%(refname:short)' | fzf --header 'git checkout' | xargs git checkout
}
# [p]ull request check[o]ut
po() {
  gh pr list --author "@me" | fzf --header 'checkout PR' | awk '{print $(NF-5)}' | xargs git checkout
}

# . /opt/homebrew/opt/asdf/asdf.sh
. $HOME/.asdf/asdf.sh

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
